description = 'Prelert Engine-Elastic integration'

apply plugin: 'maven'
apply plugin: 'java'

group = 'org.elasticsearch.prelert'
version = '0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'


repositories {
    mavenLocal()
    maven { url "http://repo.maven.apache.org/maven2" }
}

dependencies {
    compile group: 'log4j', name: 'log4j', version:'1.2.17'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version:'2.8.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.8.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.8.1'
    compile group: 'org.elasticsearch', name: 'elasticsearch', version:'5.0.0-beta1'
    compile group: 'org.elasticsearch.plugin', name: 'transport-netty3-client', version:'5.0.0-beta1'

    testCompile (group: 'junit', name: 'junit', version:'4.11') {
        exclude group: 'org.hamcrest', module: 'hamcrest-core'
    }
    testCompile group: 'org.elasticsearch.test', name: 'framework', version: '5.0.0-alpha5'
    testCompile group: 'org.elasticsearch', name: 'rest-api-spec', version: '5.0.0-alpha5'
    testCompile group: 'org.ini4j', name: 'ini4j', version:'0.5.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.10.19'
}


test {
    include '**/*Test.class'
}

task copyRestSpec(type: Copy) {
    from { zipTree(configurations.testCompile.files.find { it.name.contains('rest-api-spec')}) }
    include 'rest-api-spec/api/**'
    into project.sourceSets.test.output.resourcesDir
}

task startPrelert(type:JavaExec) {
    main = 'org.elasticsearch.xpack.prelert.ServerBootstrap'
    classpath = sourceSets.main.runtimeClasspath
    debug = project.hasProperty('debug-prelert') ? true : false
    systemProperties = [
            'jetty.home' : "$project.buildDir/prelert-data-dir"
    ]
}

task startPrelertInBackground() {
    doFirst {
        def t = Thread.start {
            startPrelert.execute()
        }
        sleep 6000
    }
}

task integTest(type: Test,
        dependsOn: test.dependsOn) {
    classpath = project.test.classpath
    testClassesDir = project.test.testClassesDir
    include '**/*IT.class'
    systemProperties = [
            'tests.rest.cluster': 'localhost:8080',
            'tests.rest.load_packaged' : 'false'
    ]
}
integTest.dependsOn startPrelertInBackground
startPrelertInBackground.dependsOn copyRestSpec
check.dependsOn integTest
integTest.mustRunAfter test
