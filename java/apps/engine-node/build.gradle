description = 'Prelert Engine-Elastic integration'

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'

group = 'org.elasticsearch.prelert'
version = '0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'


dependencies {
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.8.1') {
        transitive = false
    }
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.8.1') {
        transitive = false
    }
    compile group: 'org.elasticsearch', name: 'elasticsearch', version: '5.0.0'
    compile group: 'org.elasticsearch.plugin', name: 'transport-netty4-client', version: '5.0.0'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.6.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.2'
    compile group: 'net.sf.supercsv', name: 'super-csv', version:'2.4.0'

    testCompile group: 'org.elasticsearch', name: 'rest-api-spec', version: '5.0.0'
}


test {
    include '**/*Test.class', '**/*Tests.class'
    systemProperty 'tests.security.manager', 'false'
}

task copyRestSpec(type: Copy) {
    from { zipTree(configurations.testCompile.files.find { it.name.contains('rest-api-spec')}) }
    include 'rest-api-spec/api/**'
    into project.sourceSets.test.output.resourcesDir
}

task startPrelert(type:JavaExec) {
    main = 'org.elasticsearch.xpack.prelert.ServerBootstrap'
    classpath = sourceSets.main.runtimeClasspath
    debug = project.hasProperty('debug-prelert') ? true : false
    systemProperties = [
            'jetty.home' : "$project.buildDir/prelert-data-dir"
    ]
}

task startPrelertInBackground() {
    doFirst {
        def t = Thread.start {
            startPrelert.execute()
        }
        while (true) {
            sleep 500
            if (prelertStarted()) {
                break;
            }
        }
    }
}

static boolean prelertStarted() {
    try {
        return new URL("http://localhost:8080/_cat/health").text.contains("green");
    } catch (Exception e) {
        return false;
    }
}

task integTest(type: Test,
        dependsOn: test.dependsOn) {
    classpath = project.test.classpath
    testClassesDir = project.test.testClassesDir
    include '**/*IT.class'
    systemProperties = [
            'tests.rest.cluster': 'localhost:8080',
            'tests.rest.load_packaged' : 'false',
            'tests.security.manager' : 'false'
    ]
}
integTest.dependsOn startPrelertInBackground
startPrelertInBackground.dependsOn copyRestSpec
check.dependsOn integTest
integTest.mustRunAfter test
