import org.elasticsearch.gradle.precommit.PrecommitTasks

description = 'Prelert Engine-Elastic integration'

buildscript {
    repositories {
        if (System.getProperty("repos.mavenlocal") != null) {
            // with -Drepos.mavenlocal=true we can force checking the local .m2 repo which is useful for building against
            // elasticsearch snapshots
            mavenLocal()
        }
        mavenCentral()
        maven {
            name 'sonatype-snapshots'
            url "https://oss.sonatype.org/content/repositories/snapshots/"
        }
        jcenter()
    }
    dependencies {
        // Version of elasticsearch
        classpath "org.elasticsearch.gradle:build-tools:5.0.0"
    }
}

apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'elasticsearch.esplugin'

esplugin {
    name 'prelert'
    description 'Prelert Plugin'
    classname 'org.elasticsearch.xpack.prelert.PrelertPlugin'
}

group = 'org.elasticsearch.prelert'
version = '0.1'
sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'

// We need to enable this at some point
thirdPartyAudit.enabled = false

licenseHeaders {
    approvedLicenses = ['Elasticsearch Confidential']
    additionalLicense 'ESCON', 'Elasticsearch Confidential', 'ELASTICSEARCH CONFIDENTIAL'
}

dependencies {
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version:'2.8.1') {
        transitive = false
    }
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.8.1') {
        transitive = false
    }
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.6.2'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.6.2'
    compile group: 'net.sf.supercsv', name: 'super-csv', version:'2.4.0'

    testCompile group: 'org.elasticsearch', name: 'rest-api-spec', version: '5.0.0'
}

test {
    exclude '**/*NoBootstrapTests.class'
}

task noBootstrapTest(type: Test,
        dependsOn: test.dependsOn) {
    classpath = project.test.classpath
    testClassesDir = project.test.testClassesDir
    include '**/*NoBootstrapTests.class'
}
check.dependsOn noBootstrapTest
noBootstrapTest.mustRunAfter test

integTest {
    cluster {
        //setting 'useNativeProcess', 'true'
        setting 'bootstrap.seccomp', 'false'
    }
}

integTest.mustRunAfter noBootstrapTest

task copyCppDist(type: Copy) {
  from "$rootDir/cppdistribution"
  into "${project.buildDir}/engine-node"
}

project.bundlePlugin.dependsOn(copyCppDist)

bundlePlugin {
  from("${project.buildDir}/engine-node/plugins/prelert") {
    into '.'
  }
}
