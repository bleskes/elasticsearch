description = 'Builds the Prelert Engine native binaries'

import org.gradle.internal.os.OperatingSystem
import org.elastic.gradle.UploadS3Task

boolean isLinux = OperatingSystem.current().isLinux()
boolean isMacOsX = OperatingSystem.current().isMacOsX()
boolean isWindows = OperatingSystem.current().isWindows()

String artifact_name = 'prelert_cpp' +
    (isWindows ? "_windows-x86_64" : (isMacOsX ? "_darwin-x86_64" :
    (isLinux ? "_linux-x86_64" : "_sunos-x86_64")))

project.ext.make = (isMacOsX || isWindows) ? "gnumake" : (isLinux ? "make" : "gmake")
project.ext.numCpus = Runtime.runtime.availableProcessors()

task clean(type: Exec) {
    enabled = project.cppEnabled
    commandLine bash
    args '-c', 'source cpp/set_env.sh && rm -rf cppdistribution && rm -rf cpp/build && ' + make + ' clean'
    workingDir "${rootDir}"
}

task compile(type: Exec) {
    enabled = project.cppEnabled
    commandLine bash
    args '-c', 'source cpp/set_env.sh && ' + make + ' -j' + numCpus + ' objcompile'
    workingDir "${rootDir}"
}

task make(type: Exec) {
    enabled = project.cppEnabled
    commandLine bash
    args '-c', 'source cpp/set_env.sh && ' + make + ' -j' + numCpus
    workingDir "${rootDir}"
    dependsOn 'compile'
}

task strip(type: Exec) {
    enabled = project.cppEnabled
    commandLine bash
    args '-c', 'source cpp/set_env.sh && cpp/strip_binaries.sh'
    workingDir "${rootDir}"
    dependsOn 'make'
}

def zipSpec = copySpec {
  from("${rootDir}/cppdistribution") {
    into '.'
    // Don't copy Windows import libraries
    exclude "**/*.lib"
    // Don't copy the test support library
    exclude "**/libPreTest.*"
    includeEmptyDirs = false
  }
}

task buildZip(type: Zip) {
  dependsOn strip
  baseName = artifact_name
  with zipSpec
  destinationDir = file("${buildDir}/distributions")
}

configurations {
  archives
}

artifacts {
  archives buildZip
}

task test(type: Exec) {
    enabled = project.cppEnabled
    commandLine bash
    args '-c', 'source cpp/set_env.sh && ' + make + ' -j' + numCpus + ' test'
    workingDir "${rootDir}"
    dependsOn 'buildZip'
    description = 'Run cpp tests'
}

task check {
    enabled = project.cppEnabled
    dependsOn 'test'
    description = 'Run all verification tasks'
}

task assemble {
    enabled = project.cppEnabled
    dependsOn 'buildZip'
    description = 'Assemble the cpp part of prelert'
}

task build(dependsOn: [check, assemble]) {
  group = 'Build'
  description = 'Assembles and tests the cpp part of prelert'
}

task upload(type: UploadS3Task, dependsOn: [build]) {
  description = 'upload cpp zip to S3 Bucket'
  bucket 'prelert-artifacts'
  upload buildZip.outputs.files.singleFile, "maven/${project.group}/${artifact_name}/${elasticsearchVersion}/${buildZip.outputs.files.singleFile.name}"
}
